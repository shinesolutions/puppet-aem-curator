
class aem_curator::config_publish_dispatcher (
  $dispatcher_conf_dir,
  $docroot_dir,
  $exec_path,
  $virtual_hosts_dir,
  $publish_host,
  $publish_port,
  $ssl_cert,
  $tmp_dir,
  $publish_secure    = '1',
  $allowed_client    = undef,
  $apache_http_port  = '80',
  $apache_https_port = '443',
  $aem_id            = 'publish-dispatcher',
) {

  aem_resources::publish_dispatcher_set_config { 'Set puppet-aem-resources config file for publish-dispatcher':
    dispatcher_conf_dir => $dispatcher_conf_dir,
    virtual_hosts_dir   => $virtual_hosts_dir,
    docroot_dir         => $docroot_dir,
    ssl_cert            => $ssl_cert,
    allowed_client      => $allowed_client,
    publish_host        => $publish_host,
    publish_port        => $publish_port,
    publish_secure      => $publish_secure,
  } -> exec { 'httpd -k graceful':
    cwd  => $tmp_dir,
    path => $exec_path,
  } -> tcp_conn_validator { "Ensure publish-dispatcher is listening on http port ${apache_http_port}" :
    host      => 'localhost',
    port      => $apache_http_port,
    try_sleep => 5,
    timeout   => 60,
  } -> tcp_conn_validator { "Ensure publish-dispatcher is listening on https port ${apache_https_port}" :
    host      => 'localhost',
    port      => $apache_https_port,
    try_sleep => 5,
    timeout   => 60,
  }

}
